<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<ul>
<li><p>Always use <a href="https://www.excel-easy.com/vba/examples/option-explicit.html" rel="nofollow noreferrer">Option Explicit</a> and declare your variables as close as possible to their first use.</p></li>
<li><p>Fully <a href="https://www.spreadsheetsmadeeasy.com/7-common-vba-mistakes-to-avoid/" rel="nofollow noreferrer">qualify your worksheet references</a> (see #5)</p></li>
</ul>

<p>When calculating your <code>LastRowMatchC</code>, always make sure to fully qualify ALL worksheet references like this </p>

<pre><code>Worksheets("Compliance").Cells(Worksheets("Compliance").Rows.Count, 1).End(xlUp).Row
</code></pre>

<p>or</p>

<pre><code>With Worksheets("Compliance")
    LastRowMatchC = .Cells(.Rows.Count, 1).End(xlUp).Row
End With
</code></pre>

<p>(Notice the dot in front of the <code>Rows</code>) Otherwise, the the <code>Rows.Count</code> is looking at the currently active worksheet and not the one you intended. </p>

<ul>
<li>Use <code>EnableEvents</code> and <code>ScreenUpdating</code> when you're making changes directly on the worksheet.</li>
</ul>

<p>You can turn off events and screen updating before and after your loop to give a big speed boost to the reformatting:</p>

<pre><code>Application.EnableEvents = False
Application.ScreenUpdating = False

For rc = rngc.Count To 1 Step -1
    '--- do your thing here
Next rc

Application.EnableEvents = True
Application.ScreenUpdating = True
</code></pre>

<p>(More on this in the next comment)</p>

<ul>
<li><a href="https://stackoverflow.com/a/10717999/4717755">Avoid using <code>Select</code></a> and try to define any constant "magic values" in an expression. No one knows why you're looking for the value "2" here (and you may not remember a year from now). So replace the <code>MAGIC_VALUE</code> name with something meaningful to your application. </li>
</ul>

<p>A partial example using your code:</p>

<pre><code>Option Explicit

Sub InsertRows()
    Dim lastRow As Long
    With Worksheets("Compliance")
        lastRow = .Cells(.Rows.Count, "S").End(xlUp).Row
    End With

    Dim rngc As Range
    Set rngc = Worksheets("Compliance").Range("S8:S" &amp; lastRow)

    AppPerformance SetTo:=False

    Const MAGIC_VALUE As Long = 2

    Dim rc As Long
    For rc = rngc.Rows.Count To 1 Step -1
        If rngc(rc).Value = MAGIC_VALUE Then
            rngc(rc + 1).EntireRow.Insert
            Dim newRow As Range
            Set newRow = rngc(rc + 1).EntireRow
            With newRow
                With .Interior
                    .Pattern = xlSolid
                    .PatternColorIndex = xlAutomatic
                    .ThemeColor = xlThemeColorAccent1
                    .TintAndShade = 0.599993896298105
                    .PatternTintAndShade = 0
                End With
                .Borders(xlDiagonalDown).LineStyle = xlNone
                .Borders(xlDiagonalUp).LineStyle = xlNone
                With .Borders(xlEdgeLeft)
                    .LineStyle = xlContinuous
                    .ColorIndex = 0
                    .TintAndShade = 0
                    .Weight = xlThin
                End With
                '--- keep going with formatting ...
            End With
        End If
    Next rc

    AppPerformance SetTo:=True

End Sub

Private Sub AppPerformance(ByVal SetTo As Boolean)
    With Application
        .EnableEvents = SetTo
        .ScreenUpdating = SetTo
    End With
End Sub
</code></pre>
    </div>