<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>I was finding that when I inspected a control on a form for a value an error was being raised if the form had no recordset but the control had a controlsource.</p>

<p>Taking into consideration what Mathieu Guindon wrote in his answer.  I used the example given in the question to create the two functions below that I think may be of use to people reading this question!</p>

<pre><code>    Public Function HasValue(ControlObject As Control, Optional ControlsValue As Variant) As Boolean

    ' IN    ControlObject - The object to inspect to see whether it has a value
    ' OUT   TheValue     - A ByRef parameter that allows this to return the value of the object
    '                      if it is determined to have one!

    Dim HasVal As Variant

    On Error GoTo Err1

    ' The code below should always set HasVal to either be True or False
    HasVal = Null

    ' Return NULL if an object has 'no value'.
    ' (NULL chosen as this is a database environment where fields are NULL if no data is stored by them.)
    ControlsValue = Null

    If ControlObject Is Nothing Then

        HasVal = False
        ControlsValue = Null

    ElseIf TypeOf ControlObject Is Label Then

        If Len(ControlObject.Caption) = 0 Then
            HasVal = False
            ControlsValue = Null
        Else
            HasVal = True
            ControlsValue = ControlObject.Caption
        End If

    Else

        ' Note that error 2424 is raised if the Value proeprty of the following controls is
        ' referred to in code when
        ' 1. The control's 'Control Source' property is set to a field in the forms recordsource.
        ' and
        ' 2. the Form.RowSource property is empty.
        '
        ' Note a control's control source property can be set to text that does not refer to the recordset
        ' in which case no error is raised!  eg =MyFunction(1,2,3)
        ' So no test can be added here to predict when the above error (error 2424) is raised.
        '

        If TypeOf ControlObject Is TextBox _
            Or TypeOf ControlObject Is ListBox _
            Or TypeOf ControlObject Is ComboBox _
            Or TypeOf ControlObject Is CheckBox _
        Then

            On Error GoTo ErrNoValueProperty

                HasVal = Not IsNull(ControlObject.Value)
                ' Note that the above treats an empty string as being a value.
                'If ControlObject.Value = "" Then
                '    HasVal = False
                'End If

                ControlsValue = ControlObject.Value

            On Error GoTo Err1

        Else

            Err.Raise cnstUserDefinedErrorLowest + 1, "", "Unknown ControlObject."

        End If

    End If

TidyUpAfterNoValueProperty:

    HasValue = CBool(HasVal)

Exit Function

ErrNoValueProperty:

    If Err.Number = 2424 Then
        '
        ' 2424 = "The expression you entered has a field, control, or property name
        '         that This Database can't find."
        '
        ' See comments in code above for explaination!
        '
        HasVal = False
        GoTo TidyUpAfterNoValueProperty

    Else
        GoTo Err1

    End If

Err1:
    MyErrDescrition = BuildErrorDescription("HasValue", cnstModuleName)
    Err.Raise Err.Number, Err.Source, MyErrDescrition
    Exit Function
    Resume

End Function

Public Function NzForObjects(ControlObject As Object, ValueIfUnEntered As Variant) As Variant

    Dim ControlsValue As Variant

    On Error GoTo Err1

    If HasValue(ControlObject:=ControlObject, ControlsValue:=ControlsValue) Then

        NzForObjects = ControlsValue

    Else

        NzForObjects = ValueIfUnEntered

    End If


Exit Function
Err1:
    MyErrDescrition = BuildErrorDescription("NzForObjects", cnstModuleName)
    Err.Raise Err.Number, Err.Source, MyErrDescrition
    Exit Function
    Resume

End Function
</code></pre>
    </div>