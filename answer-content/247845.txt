<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>If you're going to be writing VBScript or WSScript files then you should be using <a href="https://www.vbsedit.com/" rel="nofollow noreferrer">VBEdit</a>.</p>
<p><img src="https://i.stack.imgur.com/42z8m.png" alt="Intellisense Image"></p>
<p><img src="https://i.stack.imgur.com/pn3tr.png" alt="VBEdit Image"></p>
<p>Allow it is not required you should still capitalize End, Function and Sub.</p>
<p>Wrapping each tasks in it's own Sub or Function will make it easier to read, debug and modify your code.</p>
<p>A Function that does not return a value should be a Sub.</p>
<p>Avoid editing text files line by line.  It is much more efficient to split the text into an array of lines, edit each element of the array, and then join the output.</p>
<h2>Refactored Code</h2>
<p>This code takes less than 1 second to trim each line of a 27 MB text file.</p>
<pre><code>Dim FullFileName
FullFileName = GetSharedFileName

If FileSystemObject.FileExists(FullFileName) Then
    TrimFileContents FullFileName
    MsgBox "Done"
Else
    MsgBox "File not found: " &amp; FullFileName
End If


Sub TrimFileContents(FullFileName)
    Dim Lines
    Lines = GetTextFileLines(FullFileName)
    
    Dim n

    For n = 0 To UBound(Lines)
        Lines(n) = Trim(Lines(n))
    Next
    
    OverWriteTextFile FullFileName, Join(Lines, vbNewLine)
End Sub

Function GetSharedFileName
    GetSharedFileName = Replace(GetParentFolderName,"jobs\SCRIPTS","shared\file") &amp; "\DCS.txt"
End Function

Function FileSystemObject
    Set FileSystemObject = CreateObject("Scripting.FileSystemObject")
End Function


Function GetParentFolderName

    GetParentFolderName = FileSystemObject.GetFile(Wscript.ScriptFullName).ParentFolder.Path

End Function

Function GetTextFileLines(FullFileName)
    Const ForReading = 1
    Dim Text
    With FileSystemObject.OpenTextFile(FullFileName, ForReading)
        Text = .ReadAll
        .Close
    End With
    
    If InStr(Text, vbNewLine) = 0 Then
        GetTextFileLines = Split(Text, vbLf)
    Else
        GetTextFileLines = Split(Text, vbNewLine)
    End If

End Function

Sub OverWriteTextFile(FullFileName, Text)
    Const ForWriting = 2

    With FileSystemObject.OpenTextFile(FullFileName, ForWriting)
        .Write Text
        .Close
    End With
    
End Sub
</code></pre>
    </div>