<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>Another <code>With</code> statement would make the code easier to read.</p>

<blockquote>
<pre><code>With Sheets("Pressure Log")
    lastRow = .Range("B" &amp; .Rows.Count).End(xlUp).Row    'populate next row with date/time
    .Range("B" &amp; lastRow + 1 &amp; ":G" &amp; lastRow + 1).Borders.LineStyle = xlContinuous
    With .Range("B" &amp; lastRow)
        .Offset(1) = Format(thisDate, "dddd")
        .Offset(1, 1) = Format(thisDate, "mm/dd/yyyy")
        .Offset(1, 2) = Format(thisDate, "hh:mm AM/PM")
        .Offset(1, 3).Select                      'position for data
    End With
End With
</code></pre>
</blockquote>

<p>It is better to format entire columns and rows.  These reduces the file size by combining multiple css rules.</p>

<p>There are many way to skin this cat. For variety I set a range variable ( newRow ) to the next empty row.  This allows me to uses ranges relative to the new row.</p>

<pre><code>Dim newRow As Range
With Worksheets("Pressure Log")
    Set newRow = .Range("B" &amp; .Rows.Count).End(xlUp).Offset(1).EntireRow
    newRow.Range("B1:G1").Borders.LineStyle = xlContinuous
    newRow.Range("B1:D1").Value = Now
    .Columns("C").NumberFormat = "ddd"
    .Columns("D").NumberFormat = "mm/dd/yyyy"
    .Columns("E").NumberFormat = "hh:mm AM/PM"
    .Columns("C:G").AutoFit
    newRow.Columns("E").Select
End With
</code></pre>

<p>With this setup we can copy all the formats from the row above except NumberFormats by using <code>Range().FillDown</code>.    </p>

<blockquote>
<pre><code> newRow.FillDown
</code></pre>
</blockquote>

<p>Alternately, we could copy all the formats from the row above like this:</p>

<blockquote>
<pre><code>newRow.Offset(-1).Copy
newRow.PasteSpecial Paste:=xlPasteFormats
Application.CutCopyMode = False
</code></pre>
</blockquote>

<p>Your code is pretty solid but if you truly want to learn Excel VBA I would start by watching this series:  <a href="https://www.youtube.com//watch?v=KHO5NIcZAc4&amp;index=1&amp;list=PLNIs-AWhQzckr8Dgmgb3akx_gFMnpxTN5" rel="nofollow noreferrer">Excel VBA Introduction</a>.  </p>

<p>You should also use <a href="http://rubberduckvba.com/" rel="nofollow noreferrer">Rubberduck VBA</a>.  It is a free add-in with many features to help you write better code.  The code formatter alone is invaluable.</p>

<p>Last but not least start answering questions on <a href="https://stackoverflow.com">StackOverflow</a>.  This will give you exposure to questions that you would never think to ask and solutions that you didn't know where possible. </p>
    </div>