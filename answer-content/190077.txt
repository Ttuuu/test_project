<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>It seems you didn't include <code>Option Explicit</code> at the top of the module. You always want to do that so you ensure all your variables are declared. You can have it automatically by going to Tools -&gt; Options in the VBE and checking the <strong>Require Variable Declaration</strong> option. </p>

<p>Wonderfully, you <em>have</em> defined all your variables. Good work!</p>

<h3>Structure</h3>

<p><em>But</em> your indenting is all.. not indented. Try to make it consistently indented so levels can be seen and labels will stick out. You have a little bit of excess white space, but I can't say that's a real problem. For instance</p>

<blockquote>
<pre><code>Sub DimensionFilterTransverse3(ByRef CalcWS As Worksheet, ByRef InputWS As Worksheet)

Sheets("Calculations").Range("X8").AutoFilter Field:=2, Criteria1:="&gt;=" &amp; Sheets("Input").Range("F31").Value

End Sub
</code></pre>
</blockquote>

<p>looks cleaner as</p>

<pre><code>Sub DimensionFilterTransverse2(ByRef CalcWS As Worksheet, ByRef InputWS As Worksheet)
    Sheets("Calculations").Range("X8").AutoFilter Field:=2, Criteria1:="&lt;=" &amp; Sheets("Input").Range("F34").Value
End Sub
</code></pre>

<h3>ByRef</h3>

<p>I see pretty much all of your arguments are passed <code>ByRef</code>. What you <em>probably</em> want to do is declare <code>Functions</code> that take arguments <code>ByVal</code> and return a reference you want <em>or</em> you don't need <code>ByRef</code> at all. Take this for example -</p>

<blockquote>
<pre><code>Sub DimensionFilterLongitudinal2(ByRef CalcWS As Worksheet, ByRef InputWS As Worksheet)
    Sheets("Calculations").Range("X8").AutoFilter Field:=3, Criteria1:="&lt;=" &amp; Sheets("Input").Range("F35").Value
End Sub
</code></pre>
</blockquote>

<p>You take arguments but you don't use them. Rather you'd like to do this</p>

<pre><code>Private Sub DimenstionFilterEtc(ByVal calculationRange As Range, ByVal inputRange As Range)
    calculationRange.AutoFilter field:=3, Criteria1:="&gt;=" &amp; inputRange.Value
End Sub
</code></pre>

<p>For pretty much all of your subs you pass arguments and don't use them. I <em>think</em> what happened is that you misunderstood how these arguments work.</p>

<p>Say you want to change something on Sheet1 <em>every time</em>. Well, you don't need to pass that as a reference to the function, the function already has access to that sheet because it's publicly available to it -</p>

<pre><code>Private Sub EditSheet()
    Sheet1.ClearFormatting
end Sub
</code></pre>

<p>But if you wanted to use that to change different sheets, then you need the argument -</p>

<pre><code>Private Sub EditSheet(ByVal targetSheet as Worksheet)
    targetSheet.ClearFormatting
end Sub
</code></pre>

<p>Now whatever sheet you pass will be edited, <em>and will remain edited</em> after the routine finishes. </p>

<p>Passing <em>ByVal</em> means that you are sending (a copy of) what <em>it</em> actually is as the argument. If you send it <em>ByRef</em> you send a <em>it</em> to it instead, and anything that happens to that reference carries back. For example -</p>

<pre><code>Sub main()
    Dim i As Long
    i = 2
    Dim j As Long
    j = addVal(i)
    'j = 6, i = 2
    j = AddRef(i)
    'j = 4, i = 6
End Sub
Private Function addVal(ByVal i As Long) As Long
    If i &gt; 1 Then i = i + 2
    addVal = i + 2
End Function
Private Function AddRef(ByRef i As Long) As Long
    If i &gt; 1 Then i = i + 2
    AddRef = i + 2
End Function
</code></pre>

<p>Changes made <code>ByRef</code> stick with you after the procedure ends rather than on just a copy, leaving your original as it should be.</p>
    </div>