<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>My answer is very similar to Ahmed AU with a few exceptions.<br>
- I didn't bother adding the Conditional formatting because everything on the new worksheet represents changes. 
- The other main difference is that I match the Ranges using the Range addresses.  These will automatically adjust for differences in column and rows count and starting cell.</p>

<h2>Refactored Code</h2>

<pre><code>Sub Compare2WorkSheets(ByRef Worksheet1 As Worksheet, ByRef Worksheet2 As Worksheet)
    Dim t As Double: t = Timer
    Dim Range1 As Range, Range2 As Range

    SetRanges Worksheet1, Worksheet2, Range1, Range2

    Dim Values1, Values2, Results
    Dim r As Long, c As Long, Count As Long
    Values1 = Range1.Value
    Values2 = Range2.Value
    ReDim Results(1 To UBound(Values1), 1 To UBound(Values1, 2))
    For r = 1 To UBound(Values1)
        For c = 1 To UBound(Values1, 2)
            If Values1(r, c) &lt;&gt; Values2(r, c) Then
                Count = Count + 1
                Results(r, c) = Values1(r, c) &amp; vbNewLine &amp; Values2(r, c)
            End If
        Next
    Next
    Workbooks.Add.Worksheets(1).Range(Range1.Address).Value = Results
    Debug.Print "Compare2WorkSheets: ", Worksheet1.Name; " to "; Worksheet2.Name
    Debug.Print "Runtime in Second(s):"; Round(Timer - t, 2)
    Debug.Print "Number of Cells per Worksheet: "; Range1.CountLarge
    Debug.Print "Number of Differences: "; Count
    MsgBox "There were " &amp; Count &amp; " Differences"
End Sub

Sub SetRanges(ByRef Worksheet1 As Worksheet, ByRef Worksheet2 As Worksheet, ByRef Range1 As Range, ByRef Range2 As Range)
    With Worksheet1
        Set Range1 = Union(.UsedRange, .Range(Worksheet2.UsedRange.Address))
    End With
    With Worksheet2
        Set Range2 = Union(.UsedRange, .Range(Worksheet1.UsedRange.Address))
    End With
End Sub
</code></pre>

<h2>Results</h2>

<p><a href="https://i.stack.imgur.com/Sd2gH.png" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/Sd2gH.png" alt="Immediate Window Screenshot"></a></p>
    </div>