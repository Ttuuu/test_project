<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>Firstly, as a habit, always include "Option Explicit" at the top of every module. This would force you to declare <code>WBgeral</code> (as <code>Workbook</code> would be logical),</p>

<p>I assume that you originally created this from a recorded macro. Your use of <code>.Select</code> and <code>Activate</code> are, in this case, not necessary and would act to slow your routine down.</p>

<p>Why make the sheets visible, only to make them invisible again? This represents two loops that you can do without.</p>

<p>You also have a <code>Cells</code> action towards the end of the program that is not properly qualified - which worksheet are you changing, and why? I am assuming this is to put a date of import into your workbook.</p>

<p>You also active, select, copy and paste - where as you can simply overwrite the target cells with the source values. Activating and selecting reflect human activity - but VBA code don't need these. Removing these extraneous steps will give the following code.</p>

<pre><code>Sub AtualizarRelatorioGeral()
    Application.Calculation = xlCalculationManual
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Application.DisplayAlerts = False
    SaveChanges = False

    Dim Arquivo(18) As String

    Arquivo(1) = "zpp03ontem"
    Arquivo(2) = "vl10a"
    Arquivo(3) = "mb51consumomensal"
    Arquivo(4) = "mb51repassegerado"
    Arquivo(5) = "mb52peixerev"
    Arquivo(6) = "mb52peixepro"
    Arquivo(7) = "mb52exp"
    Arquivo(8) = "mb52repassesaldo"
    Arquivo(9) = "zsd17"
    Arquivo(10) = "zsd25fat"
    Arquivo(11) = "zsd25dev"
    Arquivo(12) = "mc.9estoquecd"
    Arquivo(13) = "mc.9consumo"
    Arquivo(14) = "mc.9centro"
    Arquivo(15) = "mc.9cdhipet"
    Arquivo(16) = "mc.9valor"
    Arquivo(17) = "zpp25"
    Arquivo(18) = "mc.9produto"

    Dim WBGeral as Workbook
    Dim newWB as Workbook

    Set WBgeral = ActiveWorkbook 'Perhaps this should be "ThisWorkbook"?

    'IMPORTAR ARQUIVOS
    For i = 1 To 18
        WBgeral.Sheets(Arquivo(i)).Cells.Clear
        Set NewWB = Workbooks.OpenXML("C:\macrosm\prerelatoriolucimara\" &amp; Arquivo(i) &amp; ".xls")
        newWB.Sheets(1).Cells.Copy WBgeral.Sheets(Arquivo(i)).Cells
        newWB.Close SaveChanges:=False
    Next i
    'IMPORTAR ARQUIVOS

    WBgeral.Sheets("Principal").Cells(4, 16).Value = Date
    Application.Calculation = xlCalculationAutomatic
    Application.ScreenUpdating = True
    Application.EnableEvents = True
    Application.DisplayAlerts = True
    SaveChanges = True
End Sub
</code></pre>

<p>You don't have any error checking - what if the file does not open? What if the data is on a different sheet in there?</p>

<p>Also, why use <code>OpenXML</code> why you can just use <code>Open</code> as it is an <code>.xls</code> you are opening?</p>
    </div>