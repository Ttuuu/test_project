<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p><strong><em>First :</em></strong> I recommend you to change this line in your code in order to anybody can use it easily without changing or editing it.</p>

<pre><code>set SaveDest=C:\Users\ME\Desktop\Wallpaper\%dte%.jpg
</code></pre>

<p>by those lines :</p>

<pre><code>set WallpaperFolder=%userprofile%\Desktop\Wallpaper
If not exist "%WallpaperFolder%" MD "%WallpaperFolder%"
set SaveDest=%WallpaperFolder%\%dte%.jpg
</code></pre>

<p><strong><em>Second :</em></strong> If you want to hide the output of the console you can redirect it to <code>NUL</code> </p>

<p>for example : <code>reg add "HKEY_CURRENT_USER\Control Panel\Desktop" /v Wallpaper /t REG_SZ /d %savedest% /f&gt;nul</code></p>

<p>And this line too : <code>timeout /t 1 /nobreak&gt;nul</code></p>

<p><strong><em>Third :</em></strong> Refer to <a href="https://superuser.com/questions/398605/how-to-force-windows-desktop-background-to-update-or-refresh?answertab=active#tab-top">How to force Windows desktop background to update or refresh</a></p>

<p>You can write it simply by this line and avoid the loop : <code>RUNDLL32.EXE USER32.DLL,UpdatePerUserSystemParameters 1, True</code></p>

<p>So, the whole code can be written like this :</p>

<pre><code>@if (@a==@b) @end /*
@echo off
Title Download NASA images to use as Windows wallpaper
color 0A &amp; Mode 70,3
Set BaseUrl=https://apod.nasa.gov/apod/
set yr=%date:~8,2%
set mn=%date:~3,2%
set dy=%date:~0,2%
set dte=%yr%%mn%%dy%
set WallpaperFolder=%userprofile%\Desktop\Wallpaper
If not exist "%WallpaperFolder%" MD "%WallpaperFolder%"
set SaveDest=%WallpaperFolder%\%dte%.jpg
Set Url=%baseUrl%ap%dte%.html

setlocal
for /f "delims=" %%I in ('cscript /nologo /e:jscript "%~f0" "%Url%"') do (
    echo %%I
    set LastImg=%%I
)

set ImgUrl=%BaseUrl%%LastImg:~6%

bitsadmin.exe /transfer GETWALLPAPER "%ImgUrl%" "%SaveDest%"
echo(
echo        Please wait a while ... Update  UserSystemParameters 
reg add "HKEY_CURRENT_USER\Control Panel\Desktop" /v Wallpaper /t REG_SZ /d "%savedest%" /f&gt;nul
Timeout /t 1 /nobreak&gt;nul
RUNDLL32.EXE USER32.DLL,UpdatePerUserSystemParameters 1, True
goto :EOF

JScript */
function fetch(url) {
    var xhr=WSH.CreateObject("Microsoft.XMLHTTP");
    var dom=WSH.CreateObject('htmlfile');
    xhr.open("GET",WSH.Arguments(0),true);
    xhr.setRequestHeader('User-Agent','XMLHTTP/1.0');
    xhr.send('');
    while (xhr.readyState!=4) {WSH.Sleep(25)};
    dom.write('&lt;meta http-equiv="x-ua-compatible" content="IE=9" /&gt;');
    dom.write(xhr.responseText);
    return dom;
}
var dom=fetch(WSH.Arguments(0));
var link=dom.getElementsByTagName('IMG');
WSH.Echo(link[0].src);
</code></pre>
    </div>