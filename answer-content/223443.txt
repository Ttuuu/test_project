<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>Here's a very quick example of how you can simply assign a <code>Collection</code> and <code>Dictionary</code> directly from the parsed JSON object directly into your objects, then convert it into a 2-D array. </p>

<pre><code>Option Explicit

Public Sub test()
    Dim jsonInput As String
    jsonInput = "{ ""dataset"":[ { ""DealNo"":""11111"", ""DealDate"":""2010-01-01"", ""Quantity"":""1000"" }, { ""DealNo"":""11112"", ""DealDate"":""2010-01-02"", ""Quantity"":""2000"" } ]}"

    Dim json As Object
    Set json = ParseJson(jsonInput)

    DataToRange json
End Sub


Private Sub DataToRange(ByRef parsed As Object)
    '--- extract the array of data from the JSON input
    Dim dataset As Collection
    Set dataset = parsed("dataset")

    '--- now grab the first entry in the data set as assume all
    '    entries have the same number of fields
    Dim deal As Dictionary
    Set deal = dataset(1)

    '--- create an array to hold the converted data
    Dim dealData As Variant
    ReDim dealData(1 To dataset.Count, 0 To deal.Count - 1)

    '--- now convert the data set into an array
    Dim i As Long
    Dim j As Long
    For i = 1 To dataset.Count
        Set deal = dataset(i)
        For j = 0 To deal.Count - 1
            dealData(i, j) = deal.Items(j)
        Next j
    Next i

    '--- now set up the destination range and copy the deal data
    '    to that range
    Dim dealArea As Range
    Set dealArea = Sheet1.Range("A1").Resize(UBound(dealData, 1), UBound(dealData, 2))
    dealArea.Value = dealData
End Sub
</code></pre>
    </div>