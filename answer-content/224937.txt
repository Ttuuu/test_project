<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>I want to share the result of simple tests carried out today to test limit of creating range of <code>SpecialCells(xlCellTypeVisible)</code>. Only to keep the original post length readable, I am posting it as answer.</p>

<p>I filled the range A2:A150001 with 1 n number of times (each area length) with one 0 cell and filtered for 1 with code. </p>

<pre><code>Sub FillRange()
Dim Arr(1 To 150000, 1 To 1), Rw As Long, xInt As Integer, AreaLen As Integer, Cnt As Integer
AreaLen = InputBox("Enter Each area Length", , 3)
Cnt = 0
    For Rw = 1 To 150000
        If Cnt &gt; AreaLen - 1 Then
        Cnt = 0
        xInt = 0
        Else
        Cnt = Cnt + 1
        xInt = 1
        End If
    Arr(Rw, 1) = xInt
    Next
Range("A2:A150001").Value = Arr
ActiveSheet.Range("$A$1:$A$150001").AutoFilter Field:=1, Criteria1:="1"
End Sub
</code></pre>

<p>And then used the following code in step of 1k to creating range of <code>SpecialCells(xlCellTypeVisible)</code>
and find where error 1004 creeps in</p>

<pre><code>Sub TestSpecialCellRange()
Dim Rw As Long, Rng As Range
    For Rw = 1000 To 150000 Step 1000
    Set Rng = Nothing
    On Error Resume Next
    Set Rng = Range("A2:A" &amp; Rw).SpecialCells(xlCellTypeVisible)
      If Err &lt;&gt; 0 Then
      Debug.Print "Error at " &amp; Rw &amp; " Error No " &amp; Err.Number &amp; " : " &amp; Err.Description
      Err.Clear
      On Error GoTo 0
      Exit For
      Else
      Debug.Print "Success at " &amp; Rw &amp; " Range Area Count " &amp; Rng.Areas.Count
      End If
    Next
End Sub
</code></pre>

<p>Following are the result of debug window</p>

<pre><code>At area length 1
Success at 15000 Range Area Count 7500
Success at 16000 Range Area Count 8000
Error at 17000 Error No 1004 : Microsoft Office Excel cannot create or use the data range reference because it is too complex. Try one or more of the following:
• Use data that can be selected in one contiguous rectangle.
• Use data from the same sheet.

At area length 2
Success at 23000 Range Area Count 7667
Success at 24000 Range Area Count 8000
Error at 25000 Error No 1004 : Microsoft Office Excel cannot create or use the data range reference 

At area length 3
Success at 32000 Range Area Count 8000
Error at 33000 Error No 1004 : Microsoft Office Excel cannot create or use the data range reference because it is too complex. Try one or more of the following:

At area length 4
Success at 48000 Range Area Count 8000
Success at 49000 Range Area Count 8167
Error at 50000 Error No 1004 : Microsoft Office Excel cannot create or use the data range reference 


At area length 10
Success at 88000 Range Area Count 8000
Success at 89000 Range Area Count 8091
Success at 90000 Range Area Count 8182
Error at 91000 Error No 1004 : Microsoft Office Excel cannot create or use the data range reference 

Finally at area length 19 I succeed to cover 150 K
Success at 149000 Range Area Count 7096
Success at 150000 Range Area Count 7143
</code></pre>

<p>So it may be concluded that <strong>irrespective of number of rows covered, at around 8k non contagious area of the range, the error 1004 creeps in</strong>. I also tried with covering columns of the range 1 to 5 it is always same. however this is in my old good laptop with excel 2007 only, don't know what high performance machines result would be. </p>

<p><strong>Edit:</strong> Next while <strong>testing iterating  <code>For each Cell in Range</code>  and adding to Union range</strong> (If  condition meets) with simple code like</p>

<pre><code>Set Rng = Range("A1")   ' To avoid testing "if Rng is nothing" at each row
    For Each Cel In Range("A2:A150000")
    Rw = Cel.Row
        If Cel.Value = 1 Then
        Set Rng = Union(Rng, Cel)
        End If
        If Rw Mod 1000 = 0 Then
        AreaCnt = Rng.Areas.Count
        Debug.Print " Row: " &amp; Rw &amp; " Range Area  Count : " &amp; AreaCnt &amp; " at " &amp; Timer - tm
        DoEvents
        End If
    Next
</code></pre>

<p>The results shown normal behavior of union method.   The time  taken to process 1000 number of rows increases as the range grows heavy with non contiguous areas. Time to process 1 K cell increases to around 60 sec (at start it is 0.125 sec only) when range area count grows around 1000. Unable to achieve my target of 150 K rows with 75 K non contiguous area.</p>

<p>I tweak my code to utilize 30 parameter limit of Union syntax with range array.  Encouraged by improvement in performance, I further tweak the code to utilize second level of union with range array with further plan to increase level to optimize performance.</p>

<pre><code>Sub CellUnion3()
Dim Rw As Long, Rng(1 To 30) As Range, AreaCnt As Long, Arr(1 To 150001) As Variant
Dim Cel As Range, Rslt(1 To 30) As Range, FinalRslt As Range
Dim tm As Double, Cnt As Long, Cnt2 As Long

Set FinalRslt = Range("A1")   ' to avoid testing if Rng is nothing at each row
Cnt = 0
Cnt2 = 0
tm = Timer
SecCnt = 0

    For Each Cel In Range("A2:A150000")

        If Cel.Value = 1 Then
        Cnt = Cnt + 1
        Rw = Cel.Row

            If Cnt &lt;= 30 Then
            Set Rng(Cnt) = Cel
            End If

            If Cnt = 30 Then
            Cnt = 0
            Cnt2 = Cnt2 + 1
            Set Rslt(Cnt2) = Union(Rng(1), Rng(2), Rng(3), Rng(4), Rng(5), Rng(6), Rng(7), Rng(8), Rng(9), Rng(10), _
            Rng(11), Rng(12), Rng(13), Rng(14), Rng(15), Rng(16), Rng(17), Rng(18), Rng(19), Rng(20), _
            Rng(21), Rng(22), Rng(23), Rng(24), Rng(25), Rng(26), Rng(27), Rng(28), Rng(29), Rng(30))
                If Cnt2 = 29 Then
                Cnt2 = 0
                On Error Resume Next
                Set FinalRslt = Union(FinalRslt, Rslt(1), Rslt(2), Rslt(3), Rslt(4), Rslt(5), Rslt(6), Rslt(7), Rslt(8), Rslt(9), Rslt(10), _
                Rslt(11), Rslt(12), Rslt(13), Rslt(14), Rslt(15), Rslt(16), Rslt(17), Rslt(18), Rslt(19), Rslt(20), _
                Rslt(21), Rslt(22), Rslt(23), Rslt(24), Rslt(25), Rslt(26), Rslt(27), Rslt(28), Rslt(29))
                    If Err &lt;&gt; 0 Then
                    Debug.Print " Row: " &amp; Rw &amp; " at " &amp; Timer - tm &amp; " Error: " &amp; Err.Number &amp; vbCrLf &amp; Err.Description
                    Err.Clear
                    On Error GoTo 0
                    Exit For
                    Else
                    Debug.Print " Row: " &amp; Rw &amp; " at "; Timer - tm
                    End If
                End If
            End If
         DoEvents
         End If
    Next
AreaCnt = FinalRslt.Areas.Count
Debug.Print "Completed at " &amp; Timer - tm &amp; " Row: " &amp; Rw &amp; " Range Area Count: " &amp; AreaCnt
End Sub
</code></pre>

<p><em>The code still lacks final touches to complete union at end (if end of range reaches between accumulating 30 range array) but ignored as it is only for test purpose</em></p>

<p>Few extracts of the debug log</p>

<pre><code>With contiguous area length 1  separated by 1 row 
Row: 129920 at  289.71875 
Row: 131080 at 289.765625 Error: 1004
Method 'Union' of object '_Global' failed
Completed at 289.78125 Row: 131080 Range Area Count: 32480

With contiguous area length 3  separated by 1 row 
Row: 129920 at  307.8359375 
 Row: 131080 at 307.8984375 Error: 1004
Method 'Union' of object '_Global' failed
Completed at 307.9140625 Row: 131080 Range Area Count: 32480

With contiguous area length 5  separated by 1 row, it completed 150k Row
Row: 147204 at  236.8046875 
Row: 148248 at  242.71875 
Row: 149292 at  248.2109375 
Completed at 248.2734375 Row: 150000 Range Area Count: 24882
</code></pre>

<p>With the test results, <strong>is it to conclude that Microsoft union method is incapable of creating an union range with non contiguous area count more than 32 K?</strong></p>

<p>Of course both the methods of  creating range from <code>SpecialCells</code>, union and Array range can be  combined and or tweaked to many simple workarounds. But the final question is </p>

<p>Are we really bound by 8K non contiguous area count limitation of creating range from <code>SpecialCells</code> and 32 K non contiguous area count limitation of union Range?</p>
    </div>