<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>Two things will speed up this:</p>
<ul>
<li>read the sheet data into an internal array and work on that</li>
<li>use an auxiliary dict for searching</li>
</ul>
<p>like this</p>
<pre><code>Sub list2dict()
    ' 2020-07-02
    
        Dim key As Variant
        Dim ws As Worksheet
        
        Dim NewpartsDict As Dictionary
        Set NewpartsDict = New Dictionary
    
        ' Set ws = ...
        
        ' read range data into array
        ' SpecialCells... might contain several areas!
        Dim myData
        Dim partrange As Range
        
        For Each partrange In ws.Range("D2:E36419").SpecialCells(xlCellTypeVisible).Areas
            myData = partrange
            ' store array data into auxiliary dict
            Dim i As Long
            For i = 1 To UBound(myData, 1)
                NewpartsDict(myData(i, 1)) = myData(i, 2) ' dict(key) = value
            Next i
        Next partrange

        ' update partsDict's existing entries
        For Each key In partsDict
            If NewpartsDict.Exists(key) Then  ' a.k.a. Find()
                partsDict(key) = NewpartsDict(key)
            Else
                partsDict(key) = Empty
            End If
        Next key
        
        ' optional: add new entries
        For Each key In NewpartsDict
            If Not partsDict.Exists(key) Then
                partsDict(key) = NewpartsDict(key)
            End If
        Next key
        
        ' now use the updated data in partsDict
    End Sub
</code></pre>
    </div>