<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>As AJD pointed out, using Named Ranges will make the code easier to understand, read, write and modify.  The same logic can be applied to worksheet code names.  </p>

<p>Here are the names that I used when refactoring the code:</p>

<ul>
<li>Sheets("Wire Transfer Agreement") -&gt; wsWTA</li>
<li>Sheet2 -&gt; wsWireTransferRequest1</li>
<li>Sheet3 -&gt; wsChecklistLoanClosing</li>
<li>Sheet4 -&gt; wsConfirmationOutgoing1</li>
<li>Sheet5 -&gt; wsConfirmationIncoming</li>
<li>Sheet7 -&gt; wsChecklist</li>
<li>Sheet6 -&gt; wsWireTransferAgreement</li>
<li>Sheet8 -&gt; wsChecklistInternal</li>
<li>Sheet9 -&gt; wsChecklistCashManagement</li>
<li>Sheet11 -&gt; wsWireTransferRequest2</li>
<li>Sheet12 -&gt; wsConfirmationOutgoing2</li>
<li>Sheet13 -&gt; wsWTRBrokeredInternet</li>
<li>Sheet14 -&gt; wsConfirmationOutgoing3</li>
<li>Sheet18 -&gt; wsRecurringWTR</li>
</ul>

<p>Using nested <code>Select</code> statements are particularly hard to read.  Normally I would alternate <code>Select</code> with <code>If..ElseIf..Else</code> statements but the procedure is entirely too long; so I recommend writing a subroutine for each <code>Case</code> of the top level <code>Select</code> statement (see code below).</p>

<p>I only use <code>Range.EntireRow</code> when working with <code>Range</code> variables (e.g. <code>Target.EntireRow</code>).  Using <code>Rows()</code> directly will make you code more condensed and the extra whitespace will make it easier to read. </p>

<p>Before</p>

<blockquote>
<pre><code>Range("A200:A211").EntireRow.Hidden = False
</code></pre>
  
  <p>After</p>

<pre><code>Rows("200:211").Hidden = False
</code></pre>
</blockquote>

<p><code>Application.ScreenUpdating = True</code> is no longer required.  <code>ScreenUpdating</code> now resumes after the code has finished executing.  </p>

<h2>Refactored Code</h2>

<pre><code>Private Sub Worksheet_Change(ByVal Target As Range)
    Application.ScreenUpdating = False
    Application.EnableEvents = False
    Dim Unique_Identifier As String
    Dim Wire_Type As String

    Select Case Target.Address
        Case Is = "$B$4"
            EntryB4
        Case Is = "$B$5"
            EntryB5
        Case Is = "$B$6"
            EntryB6
        Case Is = "$B$7"
            EntryB7
        Case Is = "$B$8"
            EntryB8
        Case Is = "$B$9"
            EntryB9
        Case Is = "$B$10"
            EntryB10
        Case Is = "$B$11"
            EntryB11
            Rem Wires from Deposit Account or Loan (Post-Closing) Section
        Case Is = "$B$205"
            EntryB205
        Case Is = "$B$227"
            EntryB227
        Case Is = "$B$269"
            EntryB269
            Rem Loan-Closing Wires Section
        Case Is = "$B$306"
            EntryB306
        Case Is = "$B$331"
            EntryB331
        Case Is = "$B$373"
            EntryB373
            Rem Cash Management Wires Section
        Case Is = "$B$406"
            EntryB406
        Case Is = "$B$425"
            EntryB425
            Rem Internal Foresight Wires Section
        Case Is = "$B$610"
            EntryB610
            Rem Wire Transfer Agreement Section
        Case Is = "$B$5004"
            EntryB5004
            Rem Recurring Wire Transfer Request Section
        Case Is = "$B$5104"
            EntryB5104
        Case Is = "$B$5118"
            EntryB5118
    End Select

    Rem CIF Calls
    If Not Intersect(Target, Range("B103")) Is Nothing Then CIFIncoming
    If Not Intersect(Target, Range("B206")) Is Nothing Then CIFOutD
    If Not Intersect(Target, Range("B307")) Is Nothing Then CIFOutL
    If Not Intersect(Target, Range("B407")) Is Nothing Then CIFOutCM
    If Not Intersect(Target, Range("B506")) Is Nothing Then CIFBrokered

    Application.EnableEvents = True

End Sub

Sub EntryB4()
    With wsDataEntry
        .Activate
        Hide_All
        Select Case .Range("B4")
            Case Is &lt;&gt; ""
                .Rows("100:199").Hidden = False
                .Range("B101").Select
                wsConfirmationIncoming.Visible = xlSheetVisible
                .Range("B5") = ""
            Case Else
                .Range("B5").Select
        End Select
    End With
End Sub

Sub EntryB5()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B5")
            Case Is &lt;&gt; ""
                .Rows("200:211").Hidden = False
                .Rows("216:227").Hidden = False
                .Range("B201").Select
                With ThisWorkbook
                    wsChecklist.Visible = xlSheetVisible
                    wsConfirmationOutgoing1.Visible = xlSheetVisible
                    wsWireTransferRequest1.Visible = xlSheetVisible
                End With

                Select Case .Range("B5")
                    Case Is &gt; 1
                        .Rows("200:299").Hidden = False
                        Unique_Identifier = .Range("B5").Value
                        Wire_Type = "Deposit/Loan"
                        Call Find_Recurring(Unique_Identifier, Wire_Type)
                End Select

            Case Else: .Range("B6").Select
        End Select
    End With
End Sub

Sub EntryB7()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B7")
            Case Is &lt;&gt; ""
                .Rows("400:411").Hidden = False
                .Rows("414:499").Hidden = False
                .Range("B401").Select
                With ThisWorkbook
                    wsChecklistCashManagement.Visible = xlSheetVisible
                    wsConfirmationOutgoing3.Visible = xlSheetVisible
                End With
            Case Else: .Range("B8").Select
        End Select
    End With
End Sub

Sub EntryB8()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B8")
            Case Is &lt;&gt; ""
                .Rows("500:599").Hidden = False
                .Range("B501").Select
                With ThisWorkbook
                    wsWTRBrokeredInternet.Visible = xlSheetVisible
                End With
            Case Else: .Range("B9").Select
        End Select
    End With
End Sub

Sub EntryB9()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B9")
            Case Is &lt;&gt; ""
                .Rows("600:610").Hidden = False
                .Range("B601").Select
                wsChecklistInternal.Visible = xlSheetVisible

                Select Case .Range("B9")
                    Case Is &gt; 1
                        .Rows("600:699").Hidden = False
                        Unique_Identifier = .Range("B9").Value
                        Wire_Type = "Internal"
                        Call Find_Recurring(Unique_Identifier, Wire_Type)
                End Select

            Case Else: .Range("B10").Select
        End Select
    End With
End Sub

Sub EntryB10()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B10")
            Case Is &lt;&gt; ""
                Sheet6.Visible = xlSheetVisible
                wsWTA.Visible = True
                .Rows("5000:5099").Hidden = False
                .Rows("5005:5011").Hidden = True
                .Range("B5001").Select
            Case Else: .Range("B11").Select
        End Select
    End With
End Sub

Sub EntryB11()
    With wsDataEntry
        Hide_All
        .Activate
        Select Case .Range("B11")
            Case Is &lt;&gt; ""
                wsRecurringWTR.Visible = xlSheetVisible
                .Rows("5100:5118").Hidden = False
                .Rows("5111:5114").Hidden = True
                .Range("B5101").Select
            Case Else: .Range("B11").Select
        End Select
    End With
End Sub

Rem Wires from Deposit Account or Loan (Post-Closing) Section
Sub EntryB205()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B205"))
            Case Is = "yes"
                .Rows("212:215").Hidden = False
            Case Else
                .Rows("212:215").Hidden = True
                .Range("B206").Select
        End Select
    End With
End Sub

Sub EntryB227()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B227"))
            Case Is = "domestic"
                .Rows("222:243").Hidden = False
                .Rows("267:299").Hidden = False
                .Rows("244:266").Hidden = True
                .Range("B229").Select
            Case Is = "international"
                .Rows("244:299").Hidden = False
                .Rows("228:243").Hidden = True
                .Range("B245").Select
            Case Is &lt;&gt; "international", "domestic"
                .Rows("228:299").Hidden = True
                .Range("B227").Select
        End Select
    End With
End Sub

Sub EntryB269()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B269"))
            Case Is = "yes"
                wsWTA.Visible = True
                .Rows("5000:5099").Hidden = False
                .Rows("282:299").Hidden = True

                .Range("B5001").Select
            Case Else
                wsWTA.Visible = False
                .Rows("5000:5099").Hidden = True
                .Rows("281:299").Hidden = False
                .Range("B270").Select
        End Select
    End With
End Sub

Rem Loan-Closing Wires Section
Sub EntryB306()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B306"))
            Case Is = "yes"
                .Range("A313:A316,A331").EntireRow.Hidden = False
            Case Else
                .Rows("313:316").Hidden = True
                .Rows(331).Hidden = False
                .Range("B307").Select
        End Select
    End With
End Sub

Sub EntryB331()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B331"))
            Case Is = "domestic"
                .Rows("332:347").Hidden = False
                .Rows("370:399").Hidden = False
                .Rows("348:369").Hidden = True
                .Range("B331").Select
            Case Is = "international"
                .Rows("347:399").Hidden = False
                .Rows("332:346").Hidden = True
                .Range("B349").Select
            Case Is &lt;&gt; "domestic", "international"
                .Rows("332:399").Hidden = True
                .Range("B331").Select
        End Select
    End With
End Sub

Sub EntryB373()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B373"))
            Case Is = "yes"
                wsWTA.Visible = True
                .Rows("5000:5099").Hidden = False
                .Rows("383:399").Hidden = True

                .Range("B5001").Select
            Case Else
                wsWTA.Visible = False
                .Rows("5000:5099").Hidden = True
                .Rows("383:399").Hidden = False
                .Range("B374").Select
        End Select

    End With
End Sub

Rem Cash Management Wires Section
Sub EntryB406()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B406"))
            Case Is = "yes"
                .Rows("412:413").Hidden = False
            Case Else
                .Rows("412:413").Hidden = True
                .Range("B407").Select
        End Select
    End With
End Sub

Sub EntryB425()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B425"))
            Case Is = "yes"
                .Rows("430:431").Hidden = False
            Case Else
                .Rows("430:431").Hidden = True
                .Range("B426").Select
        End Select
    End With
End Sub

Rem Internal Foresight Wires Section
Sub EntryB610()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B610"))
            Case Is = "domestic"
                .Rows("611:625").Hidden = False
                .Rows("648:699").Hidden = False
                .Rows("626:647").Hidden = True
                .Range("B612").Select
            Case Is = "international"
                .Rows("626:699").Hidden = False
                .Rows("611:625").Hidden = True
                .Range("B627").Select
            Case Is &lt;&gt; "international", "domestic"
                .Rows("611:699").Hidden = True
                .Range("B610").Select
        End Select

    End With
End Sub

Rem Wire Transfer Agreement Section
Sub EntryB5004()
    With wsDataEntry
        .Activate
        .Rows("5005:5011").Hidden = True
        .Range("B5004").Select
        Select Case LCase(.Range("B5004"))
            Case Is = "entity"
                .Rows("5007:5011").Hidden = False
                .Range("B5007").Select
            Case Is = "individual(s)"
                .Rows("5005:5006").Hidden = False
                .Range("B5005").Select
        End Select
    End With
End Sub

Rem Recurring Wire Transfer Request Section
Sub EntryB5104()
    With wsDataEntry
        .Activate
        .Rows("5111:5114").Hidden = True
        .Range("B5105").Select
        Select Case LCase(.Range("B5104"))
            Case Is = "yes"
                .Rows("5111:5114").Hidden = False
                .Range("B5105").Select
            Case Is = "no"
                .Rows("5111:5114").Hidden = True
                .Range("B5105").Select
        End Select
    End With
End Sub

Sub EntryB5118()
    With wsDataEntry
        .Activate
        Select Case LCase(.Range("B5118"))
            Case Is = "domestic"
                .Rows("5119:5131").Hidden = False
                .Rows("5132:5199").Hidden = True
                .Rows(5150).Hidden = False
                .Range("B5120").Select
            Case Is = "international"
                .Rows("5119:5131").Hidden = True
                .Rows("5132:5149").Hidden = False
                .Rows("5151:5199").Hidden = True
                .Range("B5133").Select
            Case Is &lt;&gt; "international", "domestic"
                .Rows("5119:5199").Hidden = True
                .Range("B5118").Select
        End Select
    End With
End Sub
</code></pre>
    </div>