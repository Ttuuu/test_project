<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>This minimizes the interaction with the range (as mentioned by @M.Doerner)<br>
and clears the previous data in one operation (mentioned by @Patrick)</p>

<ul>
<li>Determines the range to work with

<ul>
<li>Minimum between <code>arr().Rows</code> and <code>nRows</code>, and <code>arr().Cols</code> and <code>nCols</code></li>
<li>Establishes the <code>UsedRange</code> based on <code>startCell</code>, and Cells(<code>nRows</code>, <code>nCols</code>)</li>
</ul></li>
<li>Clears entire <code>UsedRange</code></li>
<li>If <code>fromTop</code> is <code>True</code> places <code>arr()</code> into the <code>UsedRange</code></li>
<li>Else, reverses rows in <code>arr()</code> into variant array <code>result</code>, then places it into <code>UsedRange</code></li>
</ul>

<hr>

<pre><code>Rows: 100,001; Cols: 20; fromTop: True  - Time: 4.461 sec
Rows: 100,001; Cols: 20; fromTop: False - Time: 4.734 sec
</code></pre>

<hr>

<pre><code>Option Explicit

'writes the 2-D arr to startCell; if fromTop is False it starts from the last row
Public Sub WriteArrToWS(ByRef arr() As Variant, ByRef startCell As Range, _
                        ByVal fromTop As Boolean, nRows As Long, nCols As Long)

    nRows = IIf(nRows &lt; UBound(arr, 1), nRows, UBound(arr, 1))
    nCols = IIf(nCols &lt; UBound(arr, 2), nCols, UBound(arr, 2))

    Dim ws As Worksheet, fr As Long, fc As Long, ur As Range

    Set ws = startCell.Worksheet
    fr = startCell.Row
    fc = startCell.Column
    Set ur = ws.Range(ws.Cells(fr, fc), ws.Cells(fr + nRows - 1, fc + nCols - 1))
    ur.ClearContents

    Dim result As Variant, r As Long, c As Long, lr As Long

    result = ur
    result = arr            'Defaults to fromTop = True
    If Not fromTop Then     'If fromTop = False, reverses the rows in the array
        lr = nRows
        For r = 1 To nRows
            For c = 1 To nCols
                result(r, c) = arr(lr, c)
            Next
            lr = lr - 1
        Next
    End If
    ur = result
End Sub
</code></pre>
    </div>