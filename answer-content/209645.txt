<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>All zero-sized files will have the same checksum using the same given hash algorithm. Hence, use the following batch-script:</p>

<pre><code>@ECHO OFF
SETLOCAL EnableExtensions
for /f %%G in ("%~1") do set "_len=%%~zG"
if %_len% EQU 0 (
  echo MD5 hash of %1
  echo D41D8CD98F00B204E9800998ECF8427E
  echo CertUtil bypassed for zero file size
  echo D41D8CD98F00B204E9800998ECF8427E|clip  
) else (
  for /f "tokens=*" %%a in ('certutil -hashfile "%~1" MD5') do (
    echo %%a
    for /f "delims=" %%b in ('echo %%a^|findstr /r /i /x ^[0-9A-F]*[0-9A-F]$') do (
      if "%%b" neq "" echo %%b|clip
    )
  )
)
pause
</code></pre>

<p>Note that <em><code>%%~zG</code> expands <code>%%G</code> to size of file</em> (read <code>call /?</code>).</p>

<p><code>if "%%b" neq "" echo %%b|clip</code> seems to be superfluous then. Simple <code>echo %%b|clip</code> should suffice.</p>

<p>Sample output:</p>

<pre><code>==&gt; D:\bat\CodeReview\179105.bat out.txt
MD5 hash of out.txt
D41D8CD98F00B204E9800998ECF8427E
CertUtil bypassed for zero file size
Press any key to continue . . .

==&gt; D:\bat\CodeReview\179105.bat D:\bat\CodeReview\179105.bat
MD5 hash of D:\bat\CodeReview\179105.bat:
c2a4cabffac79a26ee5ed7c97cefb44b
CertUtil: -hashfile command completed successfully.
Press any key to continue . . .

==&gt;
</code></pre>
    </div>