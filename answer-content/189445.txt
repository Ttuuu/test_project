<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>What you're looking for is to <a href="https://msdn.microsoft.com/en-us/vba/excel-vba/articles/application-screenupdating-property-excel" rel="nofollow noreferrer">turn off screenupdating</a>, <a href="https://msdn.microsoft.com/en-us/vba/excel-vba/articles/application-displayalerts-property-excel" rel="nofollow noreferrer">turn off alerts</a> and auto-save and <a href="https://msdn.microsoft.com/en-us/vba/excel-vba/articles/workbook-close-method-excel" rel="nofollow noreferrer">close</a>.</p>

<pre><code>    Application.DisplayAlerts = False
    ThisWorkbook.Save
    ThisWorkbook.Close False
</code></pre>

<p>So, like this -</p>

<pre><code>Option Explicit

Sub SplitSheetsToBooks()
    Application.ScreenUpdating = False
    Application.DisplayAlerts = False
    Const PATH_TO As String = "C:\TEMP\"
    Const EXCEL_EXTENTION As String = ".xlsx"

    Dim index As Long
    Dim numberOfSheets As Long
    Dim targetSheet As Worksheet
    numberOfSheets = ThisWorkbook.Sheets.Count

    For index = 1 To numberOfSheets
        Set targetSheet = ThisWorkbook.Sheets(index)
        targetSheet.Copy
        ActiveWorkbook.SaveAs PATH_TO &amp; targetSheet.Name &amp; EXCEL_EXTENTION
        ActiveWorkbook.Close False
    Next
    Application.DisplayAlerts = True
    Application.ScreenUpdating = True
End Sub
</code></pre>
    </div>