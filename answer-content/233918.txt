<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<h2>a. Shortest minus 1</h2>

<p>The Q4 program contains a 2-byte <code>xor dx,dx</code> that you can rapidly replace by
 the 1-byte <code>cwd</code> instruction. Just bring that <code>mov ax,484Eh</code> on top. The
 positive number in <code>AX</code> will make <code>cwd</code> clear <code>DX</code>.</p>

<pre><code>B8 4E 48   mov   ax,484Eh (*)
99         cwd
B2 FC      mov   dl,252
89 D7      mov   di,dx
D1 E8      shr   ax,1
89 45 21   mov   [di+33],ax
B8 64 62   mov   ax,6264h
AB         stosw
B8 20 26   mov   ax,2620h
FE C4      inc   ah
AB         stosw
80 C4 E2   add   ah,226
CD 21      int   21h
C3         ret
</code></pre>

<p>(*) I've had to correct a typo! You erroneously wrote 487Eh.</p>

<hr>

<h2>b. Shortest minus 3</h2>

<p>When DOS starts a .COM program the general purpose registers have a certain
 value and you can take advantage of this fact.</p>

<p><em>Please note that the values that these general purpose registers have when the program is loaded by DOS are <strong>not officially documented</strong>. I myself would certainly never rely on it for any serious program, but since this Quine project is almost always some kind of challenge (even though you say that it is not!), I believe this is a genuine opportunity to shorten the code.</em></p>

<p>Here's the list (<code>DX</code> equals <code>CS=DS=ES=SS</code>):</p>

<pre><code>AX=0000  BX=0000  CX=00FF  SI=0100  DI=FFFE  BP=091C
</code></pre>

<p>This is also true for emulators like <a href="https://www.google.com/url?sa=t&amp;rct=j&amp;q=&amp;esrc=s&amp;source=web&amp;cd=1&amp;cad=rja&amp;uact=8&amp;ved=2ahUKEwjVyqXZgbDmAhWMxIUKHfAUAZIQFjAAegQIBhAB&amp;url=https%3A%2F%2Fwww.dosbox.com%2Fdownload.php%3Fmain%3D1&amp;usg=AOvVaw38peVDaamECsmPiuqiIufd" rel="nofollow noreferrer">DOSBox 0.74</a> and <a href="https://www.google.com/url?sa=t&amp;rct=j&amp;q=&amp;esrc=s&amp;source=web&amp;cd=2&amp;cad=rja&amp;uact=8&amp;ved=2ahUKEwiegvf-gbDmAhUEtRoKHYR_D_QQFjABegQIBxAB&amp;url=http%3A%2F%2Fwww.vdosplus.org%2F&amp;usg=AOvVaw3YF00dZ4-UrTBk3tbGg-pI" rel="nofollow noreferrer">vDOS 2016.10.01</a>. They show
 the exact same numbers!</p>

<p>This is how I would write your Q4 program and bring it down to just 27 bytes:</p>

<pre><code>01 F7      add   di,si      ;This sets DI=254
FD         std
B8 40 4E   mov   ax,4E40h
D1 E8      shr   ax,1
AB         stosw            ;Space and SingleQuote
89 FA      mov   dx,di      ;Here DX=252
B8 64 62   mov   ax,6264h
AB         stosw            ;Characters d and b
B8 4E 48   mov   ax,484Eh
D1 E8      shr   ax,1
89 45 21   mov   [di+33],ax ;SingleQuote and DollarSign (*)
95         xchg  ax,bp      ;This sets AH=09h
CD 21      int   21h
C3         ret
</code></pre>

<p>(*) +33 is because <code>DI</code> points to 6 bytes before a program of 27 bytes.</p>
    </div>