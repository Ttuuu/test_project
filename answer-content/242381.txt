<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>I completely omitted the <code>:HELP</code> section by adding description to labels</p>

<pre><code>:label - Description here
</code></pre>

<p>and <a href="https://ss64.com/nt/findstr.html" rel="nofollow noreferrer"><code>FINDSTR</code></a> to search the script for descriptions.</p>

<pre><code>@echo off
SETLOCAL EnableDelayedExpansion EnableExtensions


REM Define variables
=:: User-defined
set "conf=runfig.conf"
set "#MAIN=boot bootnorm settings" MAIN section
set "#SETTINGS=memory setforgename setname saveconf sname sver" SETTINGS section
set "#DEBUG=vars refresh help exit" DEBUG section
=:: Initalized
(set LF=^
%=-----DO NOT REMOVE THIS LINE. Expands to nothing.-----=%
)
set ^"NL=^^^%LF%%LF%^%LF%%LF%^^" Escaped LF gets ignored, next LF always escaped


====:BOOTSTRAP
cls
title %sver% %sname%

if EXIST "%conf%" (set "fig=True") ELSE set "fig=false"
echo MConsole Beta
echo Config loaded? %fig%

if "%fig%" == "True" goto :confLoad
&gt;nul ping 127.1
goto :MENU


====:CONFLOAD - Load the stuffs
echo %fig%
&lt;"%conf%" (FOR %%V in (fname name mem sname sver) do (
    set/p"%%V="
    echo(!%%V!
))
title %sver% %sname%
&gt;nul ping 127.1


====:MENU - main menu
cls
echo %sver% %sname% Main Menu
echo Type 'help' for help and type 'settings' for the settings menu

set "option="
set/p"option=&gt;"

cls
=:: EXIT, a special case all by itself
if "!option!" == "exit" exit /b
FOR %%O in (%#MAIN% %#DEBUG%) do if "!option!" == "%%O" call :%%O

goto :MENU


:::::::::::::::::::::::::::::::::::::::::::::::::
::::MAIN
:boot - boot modded
echo Booting...

java -Xmx%mem%G -Xms%mem%G -jar %fname%.jar nogui
&gt;nul ping 127.1

pause &gt;nul&amp;exit /b


:bootnorm - Boot vanilla server
echo WARNING: Booting a modded server in vanilla mode can corrupt the world!

CHOICE /C yn /M "Continue?"
if ERRORLEVEL 2 goto :MENU

echo You have been warned!
echo Booting...

java -Xmx%mem%G -Xms500M -jar %name%.jar nogui
&gt;nul ping 127.1

pause &gt;nul&amp;exit /b


:settings
cls
echo %sver% %sname% Settings Menu
echo Type 'help' for help

set "option="
set/p"option=&gt;"

cls
if "!option!" == "exit" goto :MENU
FOR %%O in (%#SETTINGS% %#DEBUG%) do if "!option!" == "%%O" call :%%O

goto :settings


:
::::SETTINGS
:memory - set the memory cap
echo The current memory cap is "%mem%GB"
echo What would you like to change it to? (without the 'GB' just the number)
set/p"mem=&gt;"
echo Memory cap changed to "%mem%GB"
echo Don't forget to run saveconf if you wish to save these changes!
pause &gt;nul
goto :settings


:setforgename - set the name of the forge file
echo What do you want to set the name of the forge file to (without the .jar)
echo It is currently "%fname%"

set/p"fname=&gt;"
echo Name set to "%fname%"
echo Don't forget to run saveconf if you wish to save these changes!

pause &gt;nul&amp;exit /b


:setname - set the name of the vanilla boot file
echo What do you want to set the name of the regular file to (without the .jar)
echo It is currently "%name%"

set/p"name=&gt;"
echo Name set to "%name%"
echo Don't forget to run saveconf if you wish to save these changes!

pause &gt;nul&amp;exit /b


:saveconf - save current name(s) to file
=:: Prompt for user
echo Do you wish to save these files into config file for later?%NL%

(Forge server name) - %fname%%NL%
(Vanilla server name) - %name%%NL%
(Memory) - %mem%GB%NL%
(Server name) - %sname%%NL%
(Server version) - %sver%

CHOICE /C yn /M "Continue?"
if %ERRORLEVEL% NEQ 1 goto :settings

=:: Really save the config
&gt;"%conf%" (FOR %%V in (fname name mem sname sver) do (
echo(!%%V!
&gt;&amp;3 echo Saving "%fname%" to file...
))
echo Save complete!

pause &gt;nul&amp;exit /b


:sname - Set server name
:sver - Set server version
set "var=%0"
set "var=!var:~1!"

echo The current server name is "!%var%!"
echo What would you like to change it to?
set/p"%var%=&gt;"
echo Server name changed to "!%var%!"
echo Don't forget to run saveconf if you wish to save these changes!

pause &gt;nul&amp;exit /b



:
::::DEBUG
:vars - Variables list
echo %mem%GB%NL%
  %fname%.jar%NL%
  %name%.jar%NL%
  %sname%%NL%
  %sver%

pause &gt;nul&amp;exit /b


:refresh - Start the console again from bootstrap
goto :BOOTSTRAP


:help - Show this list
echo Help menu
findstr /R "^:" "%~f0"

pause &gt;nul&amp;cls&amp;exit /b


:exit - Exit
exit /b
</code></pre>

<hr>

<p>Rules:</p>

<h1>1. <strong><a href="https://www.youtube.com/watch?v=WWJTsKaJT_g" rel="nofollow noreferrer">NEVER</a> trust user input.</strong> Use <code>!DELAYED EXPANSION!</code></h1>

<ol start="2">
<li>Use <code>CALL</code> instead of <code>GOTO</code></li>
<li>Use <code>FOR</code> loops to shorten repetitive commands</li>
<li>Use <code>CHOICE</code> instead of <code>SET /P</code></li>
<li><p>When you want to <code>ECHO</code> a multi-line string, the <em>"normal"</em> way to do it is:</p>

<pre><code>echo foo
echo bar
echo foobar
</code></pre>

<p>but it can be very slow, depending on how many <code>ECHO</code>s there are.<br>
A faster and more readable way is to create a <a href="https://stackoverflow.com/a/4455750/12861751">line continuation character</a> yourself (<code>\n</code> + <code>^</code>):</p>

<pre><code>(set LF=^
%=-----DO NOT REMOVE THIS LINE. Expands to nothing.-----=%
)
set ^"NL=^^^%LF%%LF%^%LF%%LF%^^"
echo foo%NL%
bar%NL%
foobar
</code></pre></li>
<li><p><strong>Always</strong> use full paths (use <a href="https://www.dostips.com/forum/viewtopic.php?t=6137" rel="nofollow noreferrer">variables</a> like <code>%__CD__%</code>/<code>%__APPDIR__%</code>), <em>not</em> relative paths for external commands, but i didn't do it here</p></li>
</ol>
    </div>