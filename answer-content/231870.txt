<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>This first pattern is inspired by Freeflow:</p>

<blockquote>
<pre><code>    For Each Item In Array(objectOrValueType)
        Exit For
    Next
</code></pre>
</blockquote>

<p>The idea is to use For Each Controls loop to make the correct assignment.</p>

<p>Here I use Matt's suggested name <code>Assign</code> to touch up the API Call used in <a href="https://stackoverflow.com/a/55838668/9912714">Sancarn's answer</a></p>

<blockquote>
<pre><code>#If Win64 Then
Public Declare PtrSafe Sub Assign Lib "oleaut32.dll" Alias "VariantCopy" (ByRef Target As Variant, ByRef Source As Variant)
#Else
Public Declare Sub Assign Lib "oleaut32.dll" alias "VariantCopy" (ByRef Target As Variant, ByRef Source As Variant)
#End If
</code></pre>
</blockquote>

<h2>Tests</h2>

<pre><code>Sub TestAssign()
    Dim Item As Variant
    Dim n As Long
    For n = 0 To 10
        Assign Item, objectOrValueType(n)
        Debug.Print IsObject(Item)
    Next
End Sub

Sub TestForEach()
    Dim Item As Variant
    Dim n As Long
    For n = 0 To 10
        For Each Item In Array(objectOrValueType(n))
            Exit For
        Next
        Debug.Print IsObject(Item)
    Next
End Sub

Sub TestCast()
    Dim Item As Variant
    Dim n As Long
    For n = 0 To 10
        Cast Item, objectOrValueType(n)
        Debug.Print IsObject(Item)
    Next
End Sub

Sub Cast(ByRef Target As Variant, ByRef Source As Variant)
    For Each Target In Array(Source)
        Exit For
    Next
End Sub

Function objectOrValueType(ByVal n As Long) As Variant
    If n Mod 2 = 0 Then
        objectOrValueType = Range("A1")
    Else
        Set objectOrValueType = Range("A1")
    End If
End Function
</code></pre>
    </div>