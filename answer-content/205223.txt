<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<h2>File Paths</h2>

<p>It is best to use relative references when lining to external files.</p>

<blockquote>
<pre><code>Private Function LinkToImage(ByVal SpaceObjectType As SpaceObjectType) As String
    Dim Path As String
    Path = ThisWorkbook.Path &amp; "\"
    Select Case SpaceObjectType
    Case Alien
        LinkToImage = Path &amp; "AlienShip.jpg"
    Case Comet
        LinkToImage = Path &amp; "Comet.jpg"
    Case Star
        LinkToImage = Path &amp; "Star.jpg"
    Case Missile
        LinkToImage = Path &amp; "Missile.jpg"
    Case Ship
        LinkToImage = Path &amp; "SpaceShip.jpg"
    End Select
End Function
</code></pre>
</blockquote>

<h2>Images</h2>

<p>Consider converting the JPEG files to GIFs and deleting their backgrounds and setting the image <code>BorderStyle</code> and <code>BackStyle</code> properties to transparent.</p>

<p>I would also add an Userform to store the default images.  You could also have this form double as your SpaceObject factory.  In this way, you will not have to distribute multiple file.  </p>

<h2>SpaceObject</h2>

<p>Consider storing an internal reference to SpaceObject image and returning it as a default value.</p>

<blockquote>
<pre><code>Property Get Value() As Long
    Attribute Value.VB_UserMemId = 0
    Set Value = this.Image
End Property
</code></pre>
</blockquote>

<p>SpaceObjects should only be destroyed when they leave the board.  This will reduce the flickering.  You will have to add <code>Move</code> and <code>Intersect</code> methods to the <code>SpaceObject</code> class.  </p>

<p>The program seems to lag as more images are added to the Userform. Adding the max number images that will be needed to the userform in design mode instead of adding them at run time should result in an even frame rate.  I would think it would also improve the overall performance.</p>

<h2>Demo</h2>

<p>Just for a quick demo, I converted the images to GIFs, deleted their backgrounds and loaded them into a Userform. Ideally, I would store the images in an ImageList but I don't think that Imagelists are currently supported.  It seems that this technique is actually quite faster then loading the images from file.  To adjust for the faster loading I had to change the game's interval rate from 3 to 20 for my demo.</p>

<p><a href="https://i.stack.imgur.com/OmWd4.png" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/OmWd4.png" alt="enter image description here"></a> </p>

<p>Next I added a quick hack to make it work</p>

<blockquote>
<pre><code>Public Sub RefreshGameBoard(ByVal ControlsToAdd As Collection)
    Dim Ctrl                                                            As Image
    Dim SpaceObjectIndex                                                As Variant

    For Each Ctrl In Me.Controls
        Me.Controls.Remove Ctrl.Name
    Next Ctrl

    For SpaceObjectIndex = 1 To ControlsToAdd.Count
        Set Ctrl = Me.Controls.Add("Forms.Image.1", ControlsToAdd.Item(SpaceObjectIndex).Name, True)
        Ctrl.Left = ControlsToAdd.Item(SpaceObjectIndex).Left
        Ctrl.Top = ControlsToAdd.Item(SpaceObjectIndex).Top
        Ctrl.Height = ControlsToAdd.Item(SpaceObjectIndex).Height
        Ctrl.Width = ControlsToAdd.Item(SpaceObjectIndex).Width
        'Ctrl.Picture = LoadPicture(LinkToImage(ControlsToAdd.Item(SpaceObjectIndex).SpaceObjectType))
        CopyPicture Ctrl, ControlsToAdd.Item(SpaceObjectIndex).SpaceObjectType
        Ctrl.PictureSizeMode = fmPictureSizeModeStretch
    Next SpaceObjectIndex
End Sub

Sub CopyPicture(Ctrl As MSForms.Image, SpaceObjectType As SpaceObjectType)
    Select Case SpaceObjectType
    Case Alien
        Ctrl.Picture = DefaultImagesForm.AlienShip.Picture
    Case Comet
        Ctrl.Picture = DefaultImagesForm.Comet.Picture
    Case Star
        Ctrl.Picture = DefaultImagesForm.Star.Picture
    Case Missile
        Ctrl.Picture = DefaultImagesForm.Missile.Picture
    Case Ship
        Ctrl.Picture = DefaultImagesForm.SpaceShip.Picture
    End Select
    Ctrl.BorderStyle = fmBorderStyleNone
    Ctrl.BackStyle = fmBackStyleTransparent
End Sub
</code></pre>
</blockquote>

<p><a href="https://i.stack.imgur.com/ZdNiQ.gif" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/ZdNiQ.gif" alt="enter image description here"></a></p>

<p>Note:  The actual game does not flicker as much as the GIF.</p>

<hr>

<h2>Addendum: Collision Detection</h2>

<p><a href="https://i.stack.imgur.com/e4soH.png" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/e4soH.png" alt="enter image description here"></a></p>

<p>Added Intersects Method to <code>SpaceObject</code>
<code>DefaultImagesForm</code> new generates code for <code>SpaceObjectFactory</code></p>

<p><a href="https://drive.google.com/open?id=1tv5InIFq29GBO-5-sjNZj8PtLN_bdZ4l" rel="nofollow noreferrer">Download Sample Workbook</a></p>
    </div>