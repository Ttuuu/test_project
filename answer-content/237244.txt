<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>In a slightly different implementation from @FreeFlow's answer, this code also uses a <code>Dictionary</code> but it builds a list of duplicate tasks for each <code>Text30</code> value. Then uses that task list to set <code>Flag5</code>. Tested on a very large project (&gt;1100 tasks) it runs in less than 15 seconds.</p>

<pre><code>Option Explicit

Sub CheckForDuplicates()

    Application.Calculation = pjManual
    Application.ScreenUpdating = False

    Dim text30values As Dictionary
    Set text30values = New Dictionary

    Dim taskList As String
    With ActiveProject
        '--- for each of the text30 values, create a list of tasks that contain
        '    each value. For example, if "abc" appears in tasks 17,18,23, and 50
        Dim i As Long
        For i = 1 To .Tasks.Count
            If .Tasks(i).Text30 &lt;&gt; vbNullString Then
                If text30values.Exists(.Tasks(i).Text30) Then
                    taskList = text30values(.Tasks(i).Text30) &amp; "," &amp; i
                    text30values(.Tasks(i).Text30) = taskList
                Else
                    text30values.Add Key:=.Tasks(i).Text30, Item:=i
                End If
            End If
        Next i

        '--- now run through each entry and set the flag field for the duplicates
        '    entries with duplicates will be CSV lists, so we're looking for a comma
        For i = 0 To text30values.Count - 1
            taskList = text30values.Items(i)
            If InStr(1, taskList, ",") &gt; 0 Then
                Dim theTasks As Variant
                theTasks = Split(taskList, ",")
                Dim j As Long
                For j = LBound(theTasks) To UBound(theTasks)
                    .Tasks(CLng(theTasks(j))).Flag5 = 1
                Next j
            End If
        Next i
    End With

    Application.Calculation = pjAutomatic
    Application.ScreenUpdating = True
End Sub
</code></pre>
    </div>