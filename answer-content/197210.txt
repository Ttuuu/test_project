<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>I solved the same problem a few months ago, here's the code I used: </p>

<pre><code>name_scores_total() = name_scores_total(@__DIR__()*"/022data_names.txt")
name_scores_total(names_file::String) = name_scores_total(vec(readdlm(names_file, ',', String)))

function name_scores_total(names_list::Array{String})
    sort!(names_list)
    alphabet_order = Dict((c, UInt8(c) - UInt8('A') + 1) for c in 'A':'Z')

    total_score = 0
    for (pos, name) in enumerate(names_list)
        try
            name_score = pos * sum(alphabet_order[c] for c in name)
            total_score += name_score
        catch er
            er isa KeyError &amp;&amp; error("Only CAPITAL English letters are allowed in the file ($(name) has $(er.key) in it)")
            rethrow()
        end
    end

    total_score
end

if !isinteractive()
    println(name_scores_total())
end
</code></pre>

<p>Pretty similar to what your code does when it comes down to it (the main loop), but uses multiple dispatch to allow different types of input, and uses <code>readdlm</code> to read the file, which automatically strips the quotes around the input fields. </p>

<p>I can't say for sure that this is a "better" way of doing things since I'm also still getting a feel for the language, but at the least it just offers a different approach. </p>
    </div>