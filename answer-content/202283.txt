<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>Here is another possible solution building on your algorithm, exploiting enumeration opportunities:</p>

<pre><code>Private Function EvaluateHand(ByVal dice As Variant) As String
    Dim score As Combination
    Dim bucket() As Long
    ReDim bucket(1 To 6)
    Dim i As Long

    For i = LBound(dice) To UBound(dice)
        bucket(dice(i)) = bucket(dice(i)) + 1
    Next

    i = 1
    Do
        Select Case bucket(i)
        Case 5
            score = Yacht
        Case 4
            score = four
        Case 3
            score = score + three
        Case 2
            score = score + pair
        End Select
        i = i + 1
    Loop While i &lt; 7 And score &lt; fullhouse ' evaluation need to be continued while score is nothing / pair / three
    If score = none Then
        If bucket(1) = 0 Then
            score = bigstraight
        ElseIf bucket(6) = 0 Then
            score = smallstraight
        End If
    End If

    EvaluateHand = GetEnum(score) &amp; " "
End Function
</code></pre>

<p>For that enum need to be redefined</p>

<pre><code>Private Enum Combination
    none = 0
    three = 1
    pair = 2

    four = 5
    Yacht = 6
    twopair = 4     ' pair * 2
    fullhouse = 3   ' pair +three
    smallstraight = 7
    bigstraight = 8
End Enum
</code></pre>

<p>For that GetEnum need to be updated too:</p>

<pre><code>Private Function GetEnum(ByVal value As Combination) As String
    Select Case value
        Case none
            GetEnum = "none"
        Case pair
            GetEnum = "pair"
        Case three
            GetEnum = "three"
        Case four
            GetEnum = "four"
        Case Yacht
            GetEnum = "yacht"
        Case twopair
            GetEnum = "two-pairs"
        Case fullhouse
            GetEnum = "full-house"
        Case smallstraight
            GetEnum = "small-straight"
        Case bigstraight
            GetEnum = "big-straight"
    End Select
End Function
</code></pre>
    </div>