<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<h2>Optimizations</h2>

<p>Why are you so keen on zeroing the registers at the completion of the program? Drop those and you'll have made your first optimization already.</p>

<p>Don't use <code>mov si, 0</code> to zero the register. Use <code>xor si, si</code>. It's shorter (codesize) and faster (execution speed).</p>

<p>Instead of using <code>lea bx, array</code> use <code>mov bx, array</code> (NASM style) or <code>mov bx, offset array</code> (MASM style). The <code>mov</code> variant has a shorter encoding.</p>

<p>Addressing the array will be simpler using a single index (or base) register.</p>



<pre class="lang-none prettyprint-override"><code> ORG  100h

 mov  cl, n
 mov  ch, -128     ;Array has numbers ranging from -128 to +127
 jcxz Done         ;In case the array is empty
 mov  si, array
More:
 cmp  [si], ch
 jle  NotBigger
 mov  ch, [si]     ;New MAX goes from -128 -&gt; 4 -&gt; 69
NotBigger:
 inc  si
 dec  cl
 jnz  More
Done:
 ret               ;Back to DOS

array DB 04h, 45h, 32h, 23h, 09h
n     DB 5
</code></pre>

<hr>

<p>For a faster code, because that's what you get with <strong>less jumping around</strong> (but not necessarily less jump instructions!):</p>

<p>If the array has <strong>unsigned</strong> numbers use <code>mov ch, 0</code> and also <code>jbe More</code>:</p>

<pre class="lang-none prettyprint-override"><code> ORG  100h

 mov  cl, n
 mov  ch, 0        ;Array has numbers ranging from 0 to 255
 mov  si, array-1
More:
 inc  si
 sub  cl, 1
 jb   Done         ;Happens first time if the array is empty
 cmp  [si], ch
 jbe  More         ;NotBigger
 mov  ch, [si]     ;New MAX goes from 0 -&gt; 4 -&gt; 69
 jmp  More
Done:
 ret               ;Back to DOS

array DB 04h, 45h, 32h, 23h, 09h
n     DB 5
</code></pre>

<p>The speed gain comes from the fact that a conditional jump that is <strong>taken</strong> requires many more cycles than a conditional jump that is <strong>not taken</strong>.</p>

<hr>

<p>If the array has <strong>signed</strong> numbers use <code>mov ch, -128</code> and also <code>jle More</code>:</p>

<pre class="lang-none prettyprint-override"><code> ORG  100h

 mov  cl, n
 mov  ch, -128     ;Array has numbers ranging from -128 to +127
 mov  si, array-1
More:
 inc  si
 sub  cl, 1
 jb   Done         ;Happens first time if the array is empty
 cmp  [si], ch
 jle  More         ;NotBigger
 mov  ch, [si]     ;New MAX goes from -128 -&gt; 4 -&gt; 69
 jmp  More
Done:
 ret               ;Back to DOS

array DB 04h, 45h, 32h, 23h, 09h
n     DB 5
</code></pre>
    </div>