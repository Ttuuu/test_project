<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<h2>Constants</h2>

<blockquote>
<pre><code> Private Const DELETE_KEY As Integer = 46
 Private Const BACKSPACE_KEY As Integer = 8
</code></pre>
</blockquote>

<p>Obviously these constants refer to to KeyCodes, right?  Well yeah but I still had to check.  I would prefer to use the built in constants "vbKeyDelete" &amp; "vbKeyBack" or "KeyCodeConstants.vbKeyDelete" &amp; "KeyCodeConstants.vbKeyBack".</p>

<p>If you want to use your own names, I would have then refer to the built-in constants.</p>

<blockquote>
<pre><code>   Private Const DELETE_KEY As Integer = KeyCodeConstants.vbKeyDelete '46
   Private Const BACKSPACE_KEY As Integer = KeyCodeConstants.vbKeyBack '8
</code></pre>
</blockquote>

<h2>IsLeapYear: Function</h2>

<p>This function does not work properly.</p>

<blockquote>
<pre><code>Private Function IsLeapYear(ByVal test As Long) As Boolean
    Select Case True
        Case test Mod 400
            IsLeapYear = True
        Case test Mod 100
        Case test Mod 4
            IsLeapYear = True
    End Select
End Function
</code></pre>
</blockquote>

<p><strike>
The year is a leap year if you can divide it evenly by 4 with no remainder.</strike></p>

<blockquote>
<pre><code>Private Function IsLeapYear(ByVal Value As Long) As Boolean
    IsLeapYear = Value Mod 4 = 0
End Function
</code></pre>
  
  <p></p>
</blockquote>

<p>Note: There are some conditions for leap year that I did not meet in my original code.  However, my <code>IsValidDate()</code> replacement is correct.  I simply let the VBA for me.</p>

<blockquote>
<pre><code> Val(MonthValue) = Month(DateSerial(Val(YearValue), Val(MonthValue), Val(DayValue)))
</code></pre>
</blockquote>

<h2>IsValidDate: Property</h2>

<p>The <code>Select Case</code> seems designed to exit the property if any condition evaluates as False.   The intent is to prevent the property from returning True if the date parts are not the appropriate lengths. This does not work as intended.</p>

<blockquote>
<pre><code>Select Case False
    Case Len(YearValue) &lt;&gt; IIf(this.TwoDigitYear, 2, 4)
    Case Len(DayValue) &lt;&gt; 2
    Case Len(MonthValue) &lt;&gt; 2
    Case Else
        Exit Property
End Select
</code></pre>
</blockquote>

<p>Let's substitute these values and break down the logic case by case:</p>

<blockquote>
<pre><code>this.TwoDigitYear = True
YearValue = "18"
DayValue = "01"
MonthValue = "01"
</code></pre>
</blockquote>

<p><strong>Case 1:</strong></p>

<blockquote>
<pre><code>Case Len(YearValue) &lt;&gt; IIf(this.TwoDigitYear, 2, 4)
Case Len("18") &lt;&gt; IIf(True, 2, 4)
Case 2 &lt;&gt; 2
Case False
</code></pre>
</blockquote>

<p>Since Case 1 evaluates to False the <code>Select Case</code> breaks here skipping the <code>Case Else: Exit Property</code>.</p>

<p><strong>Case 2:</strong>   </p>

<blockquote>
<pre><code>Case Len(DayValue) &lt;&gt; 2
Case Len("01") &lt;&gt; 2
Case 2 &lt;&gt; 2
</code></pre>
</blockquote>

<p><strong>Case 3:</strong>   </p>

<blockquote>
<pre><code>Case Len(MonthValue) &lt;&gt; 2
Case Len("01") &lt;&gt; 2
Case 2 &lt;&gt; 2
</code></pre>
</blockquote>

<p>Notice that cases 2 &amp; 3 also evaluate to False; preventing the property from exiting due to improper input.</p>

<p>Both the <code>IsLeapYear()</code> and <code>IsValidDate()</code> can be replaced by ↓this code↓:</p>

<pre><code>Public Function IsValidDate() As Boolean
    IsValidDate = Len(YearValue) = IIf(TwoDigitYear, 2, 4) And _
                  Len(DayValue) = 2 And _
                  Len(MonthValue) = 2 And _
                  Val(MonthValue) = Month(DateSerial(Val(YearValue), Val(MonthValue), Val(DayValue)))
End Function
</code></pre>

<h2>Order: Property</h2>

<p>Changing the date order or delimiter should cause a value update.</p>

<p>This code sample will return an invalid date because the date was set using the default date order is <code>DateOrder.YMD</code>.</p>

<pre><code>With dateInput
    Set .Wrapping = TextBox1
    .Delimiter = "."
    .DateValue = Date
    .Order = DateOrder.MDY
End With
</code></pre>

<h2>User Experience (UX)</h2>

<p>Being able to add dates without using a delimiter is super helpful.  If you are used to doing it.  Most people are not used to it.  You could greatly improve the UX by converting the date parts and delimiter to the correct format and allow delimiters KeyCodes.</p>

<p>As a user I would like to be able to type <code>2018.01.31</code>, <code>2018.1.31</code>, <code>2018/01/31</code> or <code>18-01-31</code>  and have the code automatically correct the formats and delimiters.</p>

<p>Just to be consistent with most of my posts, I am going to make a totally ridiculous suggestion.   Add a placeholder.  Wouldn't it be great to have the empty textbox display it's date format?</p>
    </div>