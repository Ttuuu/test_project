<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>You can't read the <code>Text</code> property from a multi-cell range the same way you can with (eg) <code>Value</code>, but you <em>can</em> change the number format and set the value for the whole block using only two single operations, so something like this is much faster:  </p>

<pre><code>Public Sub RemoveNumberFormat2(ByVal columnName As String, _
                               ByVal lastColumn As Integer)
    Dim d(), rng As Range, i As Long

    Set rng = Range(columnName &amp; "2:" &amp; columnName &amp; lastColumn)

    ReDim d(1 To lastColumn - 1, 1 To 1)

    'read the text values: this you have to do in a loop...
    For i = 1 To rng.Cells.Count
        d(i, 1) = rng(i).Text
    Next i

    'these you can do as a block...
    With rng
        .NumberFormat = "@"
        .Value = d
    End With
End Sub
</code></pre>

<p>In my testing on a ~3000-row column this was about 10x faster.</p>
    </div>