<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<h2>Scripting.Dictionary</h2>

<p>The code is not using any of the Dictionary's special features.  Replacing the Scripting.Dictionaries with VBA.Collections will eliminate the external reference, make the class easier to distribute and MAC friendly. </p>

<h2>Synergies</h2>

<p>Most of the pieces in place but they just don't work together as smoothly as they could.  </p>

<p>Take a look at this code snippet from the TestFormEvents userform.  </p>

<pre><code>Private Sub Emitter_Blur(Control As Object)

    RendorEventLabel Control, Blur

    'CHANGE BORDER COLOR BACK TO A LIGHT GREY
    If TypeName(Control) = "TextBox" Then
       Control.BorderColor = 12434877
       Control.BorderStyle = fmBorderStyleNone
        Control.BorderStyle = fmBorderStyleSingle
    End If

End Sub
</code></pre>

<p>Pretty straight forward but it just handles a textbox.  What if you wanted to add effects for all 6 of the supported controls (UserForm, Textbox, Label, CommandButton, ComboBox, and Frame)?  A Select Case would help.  Of course, you would have to have to make sure that each of the Cases is cased right, <strong>no IntelliSense for strings</strong>. </p>

<pre><code>Private Sub Emitter_Blur(Control As Object)

    RendorEventLabel Control, Focus

    'CHANGE BORDER COLOR FOR TEXTBOX TO A LIGHT BLUE
    Select Case TypeName(Control)
    Case "UserForm"

    Case "TextBox"
        Control.BorderColor = 16034051
        Control.BorderStyle = fmBorderStyleNone
        Control.BorderStyle = fmBorderStyleSingle
    Case "Label"

    Case "CommandButton"

    Case "ComboBox"

    Case "Frame"

    End Select

End Sub
</code></pre>

<p>This looks pretty good but should I have added `Case "MultiPage"?  No, MultiPage is not supported <strong>There is no way to know what controls are supported without examining the source code.</strong></p>

<p>Now say that we wanted to do something a little fancier like change CommandButton's scrollbar property.  Oh, they don't have a scrollbar...ugh no IntelliSense...the Control is typed as an Object.  No problem really, we can just create a <strong>separate variable for each supported control</strong>.  Yuck!</p>

<h2>Tweaks</h2>

<p>The first thing that I would do is add another enumeration for supported controls.</p>

<pre><code>Public Enum EmitterControls
    ecUserForm
    ecTextbox
    ecLabel
    ecCommandButton
    ecComboBox
    ecFrame
End Enum
</code></pre>

<p>But how to implement the enumeration?  We could bubble it up as a parameter.  That would work but I think that there is a better way.  </p>

<p>I would make these changes to the EventListenerItem class
 - Add a ControlType Property 
 - Change the scope of the MSForms variables to Public
 - Rename the MSForms to match there Type (CommandButton As MSForms.CommandButton)
 - Retype the Control from Object to EventListenerItem 
 - Pass the instance of the EventListenerItem as Control</p>

<p>Here is how the code snippet above would look after the changes:</p>

<pre><code>Private Sub Emitter_Blur(Control As EventListenerItem)

    RendorEventLabel Control, Focus

    'CHANGE BORDER COLOR FOR TEXTBOX TO A LIGHT BLUE
    Select Case Control.ControlType
    Case EmitterControls.ecUserForm

    Case EmitterControls.ecTextbox
        Control.TextBox.BorderColor = 16034051
        Control.TextBox.BorderStyle = fmBorderStyleNone
        Control.TextBox.BorderStyle = fmBorderStyleSingle
    Case EmitterControls.ecLabel

    Case EmitterControls.ecCommandButton

    Case EmitterControls.ecComboBox

    Case EmitterControls.ecFrame

    End Select

End Sub
</code></pre>

<p>The big pay off is having IntelliSense available for writing the Select Case and accessing the MsForms control properties.</p>

<p><a href="https://i.stack.imgur.com/nLeTG.png" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/nLeTG.png" alt="Enumeration IntelliSense"></a></p>

<p><a href="https://i.stack.imgur.com/tCPsq.png" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/tCPsq.png" alt="EventListenerItem MsForms Control IntelliSense"></a></p>

<p>This setup would also allow us to add an <code>ActiveEventListenerItem</code> property to the EventListenerEmitter class, which just might come in handy.</p>
    </div>