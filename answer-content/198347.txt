<div class="s-prose s-prose__disable-spoiler-hover js-post-body" itemprop="text">
<p>My solution is based off the OP's workbook which I got from a download link in his revision that got rolled back.</p>

<h2>Iterating Over the Controls</h2>

<p>The Userform uses 27 sets of ComboBoxes and TextBoxes. The ComboBoxes are numbered [3-28, 30] and are associated with the TextBoxes [3-28, 37] respectively and are named [Control Type + Index].  I used <code>n</code> to iterate over the control and added an exception for <code>n = 30</code> -&gt; <code>Controls("TextBox" &amp; n + IIf(n = 30, 7, 8)).Value</code>   </p>

<h2>CallsTable:ListObject</h2>

<p>I wrote <code>Function MapTableRow()</code> to take advantage of the data being added to a Table/ListObject. </p>

<p><a href="https://i.stack.imgur.com/w3xGH.jpg" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/w3xGH.jpg" alt="enter image description here"></a></p>

<h2>MapTableRow():Function</h2>

<p>Adds the cell references of a row in the table paired with the cell column header name to a dictionary.  In this way, the cells of the row can be referred to by name. If the <code>RowIndex</code> is omitted, a new row will be created. </p>

<p><strong>Note</strong>: <code>MapTableRow()</code> needs to be added either to the <code>CallsUserForm</code> or a <code>Public Code Module</code> for this to work.</p>

<pre><code>Function MapTableRow(tbl As ListObject, Optional RowIndex As Long = -1) As Object
    Dim ColumnIndex As Long
    Dim dic As Object
    Set dic = CreateObject("Scripting.Dictionary")

    If RowIndex = -1 Then RowIndex = tbl.ListRows.Add.Index

    For ColumnIndex = 1 To tbl.ListColumns.Count
        dic.Add Key:=tbl.ListColumns(ColumnIndex).Name, Item:=tbl.DataBodyRange.Cells(RowIndex, ColumnIndex)
    Next
    Set MapTableRow = dic
End Function
</code></pre>

<h2>Usage</h2>

<blockquote>
<pre><code> Set map = MapTableRow(Sheet3.ListObjects(1), 1)
</code></pre>
</blockquote>

<p><a href="https://i.stack.imgur.com/wKBUI.jpg" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/wKBUI.jpg" alt="enter image description here"></a></p>

<h2>Refactored Code</h2>

<pre><code>Dim map As Object
Dim n As Long

For n = 3 To 30
    If Not n = 29 And Me.Controls("CheckBox" &amp; n).Value Then
        Set map = MapTableRow(Sheet3.ListObjects(1))
        'map("(Do t Modify).Value Product Visit") = ' 2
        'map("(Do t Modify).Value Row Checksum") = ' 3
        'map("(Do t Modify).Value Modified On") = ' 4
        'map("Modified On").Value = ' 5
        map("Date Of Call").Value = TextBox1.Value      ' 6
        map("PGP Rep").Value = ComboBox1.Value          ' 7
        map("Call Type").Value = IIf(CheckBox29.Value, CheckBox29.Caption, "Sales Call")    ' 8
        map("Company Type").Value = ComboBox3.Value     ' 9
        map("Project Discussed").Value = TextBox2.Value    ' 10
        map("Company Name").Value = TextBox3.Value      ' 11
        map("Contact").Value = TextBox4.Value           ' 12
        map("Contact Type").Value = ComboBox4.Value     ' 13
        map("Focus 10").Value = IIf(CheckBox29.Value And CheckBox1.Value, "Yes", "No")    ' 14
        map("Project Type").Value = IIf(CheckBox29.Value, ComboBox6.Value, "-")    ' 15
        map("Project Status").Value = IIf(CheckBox29.Value, ComboBox7.Value, "-")    ' 16
        map("Target Project").Value = IIf(CheckBox29.Value And CheckBox2.Value, "Yes", "No")    ' 17
        map("Project Region").Value = IIf(CheckBox29.Value, ComboBox8.Value, "-")    ' 18
        map("Project City").Value = IIf(CheckBox29.Value, TextBox5.Value, "-")    ' 19
        map("GC #01").Value = IIf(CheckBox29.Value, TextBox6.Value, "-")    ' 20
        map("GC #02").Value = IIf(CheckBox29.Value, TextBox7.Value, "-")    ' 21
        map("GC #03").Value = IIf(CheckBox29.Value, TextBox8.Value, "-")    ' 22
        map("Entertainment").Value = ComboBox5.Value    ' 23
        map("PGP Product").Value = Controls("CheckBox" &amp; n).Caption    ' 24
        map("Comments").Value = Controls("TextBox" &amp; n + IIf(n = 30, 7, 8)).Value    ' 25
        map("Article Link").Value = IIf(CheckBox29.Value, TextBox9.Value, "-")    ' 26
        map("Project Photo Link").Value = IIf(CheckBox29.Value, TextBox10.Value, "-")    ' 27
        'map("Status").Value = ' 28
    End If
Next
</code></pre>

<h2>Observations</h2>

<p>None of the controls on the userform are named.  This makes it very difficult to develop and modify the code.  </p>

<p>The current design is very rigid.  The way it is designed makes it difficult to load the data back into the userform.  It is also going to be difficult to maintain as the company grows.  </p>

<p>Alternatively. I would insert new rows of controls into a <code>MSForms.Frame</code> control and have each row of controls associated with a row in the table.</p>

<p><a href="https://i.stack.imgur.com/Zxn1Z.jpg" rel="nofollow noreferrer"><img src="https://i.stack.imgur.com/Zxn1Z.jpg" alt="enter image description here"></a></p>
    </div>